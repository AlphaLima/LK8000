apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion "25.0.1"
    defaultConfig {
        applicationId "org.lk8000"
        minSdkVersion 9
        targetSdkVersion 21
        versionCode 1
        versionName "6.1a"
        setProperty("archivesBaseName", "LK8000-$versionName")
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++11 -frtti -fexceptions"
                arguments "-DBOOST_ROOT=$System.env.BOOST_ROOT"
            }
        }
        copy {
            from "../../Common/Data/Language/Translations"
            into "src/main/assets/language"
        }
        copy {
            from "../../Common/Data/Language"
            into "src/main/assets/language"
            include "ENG_MSG.TXT"
            include "ENG_HELP.TXT"
            include "ENGLISH.LNG"
        }
    }
    sourceSets {
        main{
            java.srcDirs += [
                        "../../android/src",
                        "../../android/ioio/software/IOIOLib/src",
                        "../../android/ioio/software/IOIOLib/target/android/src",
                        "../../android/ioio/software/IOIOLibAccessory/src",
                        "../../android/ioio/software/IOIOLibBT/src",
                        "../../android/ioio/software/IOIOLibAndroidDevice/src"
                        ]
            manifest.srcFile "../../android/AndroidManifest.xml"
            res.srcDirs += ["../../android/res"]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        x86 {
            ndk {
                abiFilter "x86"
            }
        }
        x86_64 {
            ndk {
                abiFilter "x86_64"
            }
        }
        mips {
            ndk {
                abiFilter "mips"
            }
        }
        armv7 {
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        arm64 {
            ndk {
                abiFilter "arm64-v8a"
            }
        }
        arm {
            ndk {
                abiFilter "armeabi"
            }
        }
        fat {
            ndk {
                abiFilters 'x86', 'x86_64', 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips'
            }
        }
    }
    externalNativeBuild {
        cmake {
            path "../../CMakeLists.txt"
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
